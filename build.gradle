buildscript {
  dependencies {
    classpath("org.yaml:snakeyaml:2.2")
    classpath("net.serenity-bdd:serenity-gradle-plugin:4.1.3")
    classpath("au.com.dius.pact:au.com.dius.pact.gradle.plugin:4.4.0-beta.1")
  }
}

plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.4'
  id 'org.springframework.boot' version '2.7.18'
  id 'org.owasp.dependencycheck' version '9.0.5'
  id 'com.github.ben-manes.versions' version '0.50.0'
  id 'org.sonarqube' version '3.5.0.2730'
  id 'info.solidsoft.pitest' version '1.15.0'
  id 'io.freefair.lombok' version '6.6.3'
  id 'au.com.dius.pact' version '4.2.14'
  id 'org.flywaydb.flyway' version '9.22.3'
  id 'uk.gov.hmcts.java' version '0.12.50'
  id 'com.adarshr.test-logger' version '3.2.0'
}

apply plugin: 'java'
apply plugin: 'net.serenity-bdd.serenity-gradle-plugin'
apply plugin: 'project-report'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'


def versions = [
  junit                 : '5.10.1',
  junitPlatform         : '1.10.1',
  pitest                : '1.9.3',
  reformLogging         : '5.1.9',
  sonarPitest           : '0.5',
  reformHealthStarter   : '0.0.5',
  serenity              : '4.1.3',
  springDoc             : '1.7.0',
  pact_version          : '4.1.7',
  restAssured           : '5.4.0',
  serviceTokenGenerator : '4.1.2',
  qpidJmsClient         : '1.11.0'
]

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
  ]
]

mainClassName = 'uk.gov.hmcts.reform.iahearingsapi.Application'

group = 'uk.gov.hmcts.reform'
version = '1.0.0'

compileJava {
  options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

compileTestJava {
  options.compilerArgs << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  contractTest{
    java{
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    resources.srcDir file('src/contractTest/resources')
  }
}

processIntegrationTestResources {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processFunctionalTestResources {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  smokeTestImplementation.extendsFrom testImplementation
  smokeTestRuntimeOnly.extendsFrom runtimeOnly

  contractTestImplementation.extendsFrom testImplementation
  contractTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  systemProperty "scenario", System.getProperty('scenario')
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  finalizedBy aggregate
  testLogging {
    showStackTraces = true
  }
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

task contract(type: Test, description: 'Runs the consumer Pact tests', group: 'Verification') {
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

checkstyle {
  maxWarnings = 0
  toolVersion = '10.12.5'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle.xml'))
}

// make build fail on Checkstyle issues (https://github.com/gradle/gradle/issues/881)
tasks.withType(Checkstyle).each { checkstyleTask ->
  checkstyleTask.doLast {
    reports.all { report ->
      def outputFile = report.destination
      if (outputFile.exists() && outputFile.text.contains("<error ")) {
        throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
      }
    }
  }
}

pmd {
  toolVersion = "6.55.0"
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSets = [
    'category/java/errorprone.xml',
    'category/java/bestpractices.xml'
  ]
}

jacoco {
  toolVersion = "0.8.11"
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.getOutputLocation().set(file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"))
  }
}

pitest {
  junit5PluginVersion = '1.1.2'
  targetClasses = ['uk.gov.hmcts.reform.iahearingsapi.*']
  excludedClasses = [
    'uk.gov.hmcts.reform.iahearingsapi.infrastructure.config.*',
    'uk.gov.hmcts.reform.iahearingsapi.Application'
  ]
  threads = 10
  outputFormats = ['XML', 'HTML']
  timestampedReports = true
  failWhenNoMutations = false
  verbose = true
  mutationThreshold = 90
}

project.tasks['pitest'].group = "Verification"

project.ext {
  pacticipant = 'ia_hearingsApi'
  pacticipantVersion = getCheckedOutGitCommitHash()
}

project.tasks['check'].dependsOn integration

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: Immigration & Asylum Hearings Api"
    property "sonar.projectKey", "IAHEARINGSAPI"
    property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/iahearingsapi/infrastructure/config/**," +
                                 "src/main/java/uk/gov/hmcts/reform/iahearingsapi/Application.java," +
                                  "src/main/java/uk/gov/hmcts/reform/iahearingsapi/domain/entities/hmc/**"
  }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
project.tasks['sonarqube'].group = "Verification"

dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
}

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

ext {
  log4JVersion = "2.22.0"


  dependencyManagement {
    dependencies {
      // CVE-2020-15522
      dependency group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.70'
      dependency group: 'org.yaml', name: 'snakeyaml', version: '2.2'
      dependency group: 'commons-fileupload', name: 'commons-fileupload', version: '1.5'

      // CVE-2019-10086
      dependencySet(group: 'commons-beanutils', version: '1.9.4') {
        entry 'commons-beanutils'
      }

      //CVE-2021-42550
      dependencySet(group: 'ch.qos.logback', version: '1.2.13') {
        entry 'logback-classic'
        entry 'logback-core'
      }

    }
  }}

dependencies {
  modules {
    module("org.codehaus.groovy:groovy") {
      replacedBy("org.apache.groovy:groovy", "conflicts in current rest-assured version")
    }
    module("org.codehaus.groovy:groovy-xml") {
      replacedBy("org.apache.groovy:groovy-xml", "conflicts in current rest-assured version")
    }
    module("org.codehaus.groovy:groovy-json") {
      replacedBy("org.apache.groovy:groovy-json", "conflicts in current rest-assured version")
    }
  }
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-client'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-resource-server'

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '3.1.8'

  implementation group: 'org.springframework', name: 'spring-jms', version: '5.3.31'
  implementation group: 'org.springframework', name: 'spring-messaging', version: '5.3.31'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: versions.springDoc

  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'

  implementation group: 'org.apache.qpid', name: 'qpid-jms-client', version: versions.qpidJmsClient

  implementation group: 'com.google.guava', name: 'guava', version: '32.1.2-jre'
  implementation group: 'commons-io', name: 'commons-io', version: '2.15.1'
  implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '6.3.0'

  implementation group: 'com.github.hmcts', name:'service-auth-provider-java-client', version: versions.serviceTokenGenerator
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: versions.reformLogging
  implementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '4.9.2'

  implementation group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '2.3.9'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.16.0'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.16.0'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.16.0'
  implementation group: 'com.fasterxml.jackson', name: 'jackson-bom', version: '2.16.0', ext: 'pom'
  implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'

  implementation group: 'com.azure', name: 'azure-messaging-servicebus', version: '7.14.6'
  implementation group: 'com.azure', name: 'azure-core', version: '1.45.1'

  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '9.0.83'
  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: '9.0.83'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.0.1'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion

  implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'

  implementation group: 'org.webjars', name: 'webjars-locator-core', version: '0.45'
  implementation group: 'org.webjars', name: 'webjars-locator-jboss-vfs', version: '0.1.0'

  testImplementation group: 'io.rest-assured', name: 'rest-assured-all', version: versions.restAssured
  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
  testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.12.4'

  testImplementation group: 'org.apache.qpid', name: 'qpid-jms-client', version: versions.qpidJmsClient

  testImplementation libraries.junit5
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  //pact contract testing
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version

  contractTestImplementation('org.junit.jupiter:junit-jupiter-api:5.10.1')
  contractTestImplementation('org.junit.jupiter:junit-jupiter-engine:5.10.1')

  contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  contractTestImplementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'

  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath

  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.12.4'

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'

  smokeTestImplementation group: 'org.yaml', name: 'snakeyaml'

  integrationTestImplementation group: 'org.yaml', name: 'snakeyaml'

  functionalTestImplementation group: 'org.yaml', name: 'snakeyaml'
  functionalTestImplementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '4.9.2'

  contractTestImplementation group: 'org.yaml', name: 'snakeyaml'

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation sourceSets.test.runtimeClasspath

  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath
}

idea {
  module {
    testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
    testResourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
    testResourceDirs += project.sourceSets.functionalTest.resources.srcDirs
    testSourceDirs += project.sourceSets.smokeTest.java.srcDirs
    testResourceDirs += project.sourceSets.smokeTest.resources.srcDirs
    testSourceDirs += project.sourceSets.contractTest.java.srcDirs
    testResourceDirs += project.sourceSets.contractTest.resources.srcDirs
  }
}

task runAndPublishConsumerPactTests(type: Test) {
  logger.lifecycle("Runs pact Tests")
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

tasks.withType(Copy).all { duplicatesStrategy 'exclude' }

gradle.startParameter.continueOnFailure = true

static def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues.
// this setting only applies when running via gradle bootRun -- see web.config for the
// java flag that configures the deployed applications
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]

bootJar {
  archiveFileName = "ia-hearings-api.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

testlogger {
  theme 'standard'
  showExceptions true
  showStackTraces true
  showFullStackTraces false
  showCauses true
  slowThreshold 5000
  showSummary true
  showSimpleNames false
  showPassed true
  showSkipped true
  showFailed true
  showStandardStreams true
  showPassedStandardStreams true
  showSkippedStandardStreams true
  showFailedStandardStreams true
}
